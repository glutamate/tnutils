Prog. Program ::= "module" BIdent "where" "{" [Declare] "}";

DLet. Declare ::= Pat [Pat] "=" Exp ;
DLetStore. Declare ::= BIdent "!" "=" Exp ;
DImport. Declare ::= "use" BIdent ;
DType. Declare ::= BIdent "::" Type ;
DInfixl. Declare ::= "infixl" Integer Infix ;
DInfixr. Declare ::= "infixr" Integer Infix ;
DSinkConn.Declare ::= BIdent "*>" BIdent Exp;
DReadSrc.Declare ::= BIdent "<*" BIdent Exp ;
DStage.Declare ::= "stage" BIdent Integer;
DStageNeg.Declare ::= "stage" BIdent "-" Integer;

separator  Declare ";" ;

Var. Exp6 ::= BIdent ;
EWild. Exp6 ::= "_" ;
EConst. Exp6 ::= Const ;
EApp. Exp4 ::= Exp4 Exp5 ;
EInfix.  Exp1  ::= Exp1 Infix Exp2 ;
ESub. Exp1  ::= Exp1 "-" Exp2 ;
--EMul. Exp2  ::= Exp2 "*" Exp3 ;
Negate. Exp3 ::= "-" Exp4;


If. Exp ::= "if" Exp "then" Exp "else" Exp ;
Lam. Exp ::= "\\" Pat "->" Exp ;
Pair. Exp6 ::= "(" Exp "," Exp ")" ;
Pair3. Exp6 ::= "(" Exp "," Exp "," Exp ")" ;
Nil. Exp6 ::= "[]" ;
ListLit.Exp6 ::= "[" [Exp] "]" ;
--cons with infix

Sig. Exp6 ::= "{:" Exp ":}" ;
SigLimited. Exp6 ::= "{:" Exp "#" Exp ":}" ;
SigVal. Exp6 ::= "<:" Exp ":>" ;
SigFby. Exp ::= Exp1 "fby" Exp1; 

Event. Exp6 ::= "[:" Exp ":]" ;
Switch. Exp ::= "switch" "{" [SwitchLine] "}";
SwitchD1. Exp ::= "D" BIdent "=" Exp ;
SwitchD2. Exp ::= "D" "(" "D" BIdent ")" "=" Exp ;

ELet.Exp ::= "let" "{" [LetLine] "}" "in" Exp ;
ECase.Exp ::= "case" Exp "of" "{" [CaseLine] "}"  ;

coercions Exp 6 ;
separator Exp ","  ;

--D. ManyDs ::= "D" ; 

--separator ManyDs "," ; 

LetLine. LetLine ::= Pat "=" Exp ;
CaseLine. CaseLine ::= Pat "->" Exp ;
separator LetLine ";"  ;
separator CaseLine ";"  ;


SwitchLine. SwitchLine ::= [Exp] "~>" Exp ;
separator SwitchLine ";"  ;

PVar. Pat1 ::= BIdent ;
PWild. Pat1 ::= "_" ;
PLit. Pat1 ::= Const ;
PPair. Pat1 ::= "(" Pat "," Pat ")" ;
PNil. Pat1 ::= "[]" ;
PCons. Pat ::= Pat1 ":" Pat1 ;
PDeriv. Pat ::= "D" Pat1;

coercions Pat 1 ;
separator Pat "" ;

TUnit. Type2 ::= "()" ;
TyIdent. Type2 ::= BIdent;
TLam. Type ::= Type1 "->" Type ;
TPair2. Type2 ::= "(" Type "," Type ")" ;
TPair3. Type2 ::= "(" Type "," Type "," Type ")" ;
TSignal. Type1 ::= "Signal" Type2;
TEvent. Type1 ::= "Event" Type2;
TDuration. Type1 ::= "Duration" Type2;
TList. Type2 ::= "[" Type "]";

coercions Type 2 ;

CInt. Const ::= Integer ;
CDbl. Const ::= Double ;
CUnit. Const ::= "()" ;
CTrue. Const ::= "true" ;
CFalse. Const ::= "false" ;
CString. Const ::= String ; 


comment "--" ;

comment "{-" "-}" ;

layout "where", "of", "let", "switch";
layout stop "in";

token BIdent ((letter|'_') (letter|digit|'_'|'\'')*) ;
token Infix (([".:^*+=<>&%$!#%|/\\"]) ([".:-^*+=<>&%$!#%|/\\"]*)) ; --may not start with -
